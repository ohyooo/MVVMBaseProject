plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
}

android {
    signingConfigs {
        config {
            enableV3Signing true
            enableV4Signing true
        }
        release {
            storeFile file('../signkey.jks')
            storePassword '123456'
            keyAlias = 'demo'
            keyPassword '123456'
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }
    buildTypes {
        debug {
            // minifyEnabled true
            // zipAlignEnabled true
            // shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
        useIR = true
    }
    kapt {
        useBuildCache = true
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    android.applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {
            variant.outputs.all {
                outputFileName = "BaseProject_v${defaultConfig.versionName}b${defaultConfig.versionCode}_${variant.buildType.name}_${getDate()}.apk"
            }
        }
    }
}

private static def getDate() {
    return new Date().format('yyyyMMddHHmm')
}

dependencies {
    implementation project(':lib')
    implementation project(':network')
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0-beta01'
    //
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //
    implementation 'com.jakewharton.timber:timber:4.7.1'
}
